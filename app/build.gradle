plugins {
    alias(libs.plugins.android.application)
    id 'com.google.gms.google-services'
}

android {
    namespace 'com.northcoders.jvevents'
    compileSdk 35

    defaultConfig {
        applicationId "com.northcoders.jvevents"
        minSdk 24
        targetSdk 35
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Inject GOOGLE_WEB_CLIENT_ID from local.properties into BuildConfig
        def localProps = new Properties()
        def localPropsFile = rootProject.file("local.properties")
        if (localPropsFile.exists()) {
            localProps.load(new FileInputStream(localPropsFile))
        }
        def googleClientId = localProps.getProperty("GOOGLE_WEB_CLIENT_ID", "")
        buildConfigField "String", "GOOGLE_WEB_CLIENT_ID", "\"${googleClientId}\""

        packagingOptions {
            exclude 'META-INF/DEPENDENCIES'
            exclude 'META-INF/NOTICE'
            exclude 'META-INF/LICENSE'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        dataBinding true
        viewBinding true
        buildConfig true
    }
}

dependencies {
    // Core UI & Layout
    implementation libs.glide
    implementation libs.appcompat
    implementation libs.material
    implementation libs.activity
    implementation libs.constraintlayout

    // Firebase
    implementation libs.firebase.auth
    implementation platform(libs.firebase.bom)

    // Google
    implementation libs.google.auth
    implementation libs.google.wallet

    // Navigation
    implementation libs.navigation.fragment
    implementation libs.navigation.ui

    // Networking
    implementation libs.retrofit
    implementation libs.converter.gson
    implementation libs.okhttp
    implementation libs.okhttp.urlconnection
    implementation libs.logging.interceptor

    // Testing
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}